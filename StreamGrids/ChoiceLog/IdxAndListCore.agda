-- Module      : StreamGrids.ChoiceLog.Core
-- Description : Core definitions and functions
-- Copyright   : (c) Lulof Pirée, 2025
-- License     : AGPL-v3
-- Maintainer  : Lulof Pirée
-- Stability   : experimental
--------------------------------------------------------------------------------
-- Intrinsic "choice-log" representation of StreamGrid states: 
-- an inductive type where all constraints are part of the constructors.
--
-- This is in contrast with the list-of-lists representation of states,
-- where all invariants of the states are added externally via a big nesting of
-- Σ's stating properties about the list-of-lists.
-- The latter approach became very cumbersome when computing a new state,
-- as all the external properties needed to be reproven,
-- which turned out to be complicated.
--
-- In the current representation, a state is essentially a stack of choices.
-- A successor state just adds an allowed choice on top of the stack,
-- making it much easier to prove that all previous properties are still
-- preserved.
--
-- "Substacks" version of implementation of normal form computation.
-- * Elements of a state are sub-choice-logs, or just substacks of choices.
-- * States are not indexed.
-- * States are still represented by a choice log, not a list of lists.
-- * The is-a-substack-relation is defined and heavily employed.
-- * Normal forms ... #TODO
-- The hope is that substacks are easier to work with than with numbers.
-- Especially the finite sets were an infinite source of headaches in
-- the previous attempt, "PhCore".
--
-- "NewSCore" version of normal form computation using NewSignoid
-- as definition of Signoids, and non-contractible LegalChoices.
--
-- "NFListCore" version improves "NewSCore" by annotating SGState's with a list
-- of normal forms. This is necessary to avoid circular dependencies
-- between the definition of IsNF and LegalChoices (making IsNF an inductive
-- type and using mutual induction does not fix it; it becomes
-- non-strictly-positive).
-- Normal forms are identified with their index in the enumeration,
-- and this representation is stored in the list.
-- This version makes IsNF no longer needed, checking normality becomes trivial.
-- * _⊑_ needs to be proven to be Well-Founded.
-- * Some auxiliary lemmas about list indices are needed.
--
-- "NFListIElemCore" changes the definition of LegalChoices
-- to use iElem rather than sElem again (i.e., representing 
-- elements in choice logs by their enumeration-index rather than
-- as sub-choice-logs).
--
-- "IdxAndListCore" version (16 Dec 2025) again adds a numerical index to
-- states, the enumeration index of the last element added (this is off-by-one
-- in comparison with the numerical index used before the "Substacks" version).
-- This is necessary to be able to avoid building longer choicelogs than there
-- exist distinct elements, and to compute the height and the
-- next-element-to-add from a state, because of the following:
-- * the `choose` constructor of SGStates should not allow adding choices
--      to a stack that already has all elements of A. 
--      So it asks for a proof that the height of the input stack is not max.
-- * the `height` function would proceed by structural induction to compute the
--      number of choices in a choicelog.
-- These definitions are defined in terms of each other, this circularity
-- breaks things. Hence the solution is to cache the index of the last element
-- added during the construction of the choice log (like how the NFList does it
-- for normal forms). This gives enough information to quickly check if another
-- element can be added or not.

{-# OPTIONS --allow-unsolved-metas #-}

module StreamGrids.ChoiceLog.IdxAndListCore where

-- Certainly used standard library imports.
open import Level
open import Relation.Binary
open import Relation.Nullary
open import Relation.Binary.PropositionalEquality
open ≡-Reasoning
open import Data.Product
open import Data.Nat
open import Data.Nat.Properties
open import Data.Sum
open import Data.Fin
open import Data.Fin.Properties
open import Data.Unit
open import Data.Empty
open import Data.List
--open import Data.List.Relation.Unary.Sorted.TotalOrder
open import Data.List.Relation.Unary.AllPairs using (AllPairs)
open import Data.List.Relation.Unary.All using (All)
open import Data.List.Relation.Binary.Suffix.Heterogeneous using (Suffix)
open import Data.List.Membership.Propositional using (_∈_ ; _∉_ )
open import Data.List.Membership.Propositional.Properties using (∈-lookup)
open import Data.List.Relation.Unary.Any using (Any)

-- Certainly used local imports.
open import StreamGrids.NewSignoid
open import StreamGrids.Card
open import StreamGrids.Suffix
open import StreamGrids.Logic

module SGStates
    {ℓ : Level}
    {A : Set ℓ}
    {_⊂_ : Rel A ℓ}
    (S : Signoid _⊂_)
    where

    -- Setting some abbreviations for some of the data of the Signoid
    -- for better readability.
    private 
        card : ℕ∞
        card = Signoid.card S

        -- Existing indices in the enumeration of A.
        -- That's ℕ if A has infinitely many elements
        -- and Fin n otherwise.
        C : Set
        C = cardToSet card

        idxSuc : {i : C} → (h : IsNotMax i) → C
        idxSuc {i} h = endoSuc {card} {i} h

        -- Default _<_ relation on `C`, which is either Fin._<_
        -- or ℕ._<_ (or just ⊥ if card = zero).
        _<C_ : Rel C _
        _<C_ = cardTo< {card}

        _>C_ : Rel C _
        _>C_ = λ i → λ j → j <C i

        -- Idem for ≤.
        _≤C_ : Rel C _
        _≤C_ = cardTo≤ {card}

        NFList : Set
        NFList = List C

        idxToEl : C → A
        idxToEl = Signoid.idxToEl S

        elToIdx : A → C
        elToIdx = Signoid.elToIdx S

        invIdxElIdx
            : (i : C)
            → (elToIdx (idxToEl i)) ≡ i
        invIdxElIdx i = 
            let h = proj₂ (Signoid.inv S) in
            h {i} {idxToEl i} refl

    -- These inductive types are defined via mutual induction,
    -- so we declare them all up front here.
    data SGState : C → NFList → Set ℓ


    Q : Set _
    Q = Σ[ i ∈ C ](Σ[ L ∈ NFList ](SGState i L))

    -- Get the enumeration-index of the last element added to a choicelog.
    idx : Q → C
    idx (i , _ , _) = i

    -- Get the list of normal forms of a choicelog.
    nflist : Q → NFList
    nflist (_ , L , _) = L

    -- Get the last element added to a choicelog.
    el : Q → A
    el q = idxToEl (idx q)

    data LegalChoices : Q → Set ℓ
    UpdateNFList : (q : Q) → (h : IsNotMax (idx q)) → LegalChoices q → NFList
      
    -- Strict Is-a-sub-ChoiceLog-of relation.
    -- I made custom `\subst` binding in my nvim/Cornelis setup.
    -- for the `⋤` symbol.
    data _⋤_ : Rel Q ℓ

    -- Is-a-sub-ChoiceLog-of relation.
    -- A state/ChoiceLog q is a stack of choices,
    -- and q' ⊑ q denotes simply that q' is a substack of q.
    -- This relation forms a poset: reflexive, transitive, antisymmetric.
    -- `⊑` in Cornelis: `\sqsubseteq` or `\squb=`. 
    -- I made custom `\substeq` binding in my setup.
    -- Note: for ℕ, < is defined in terms of ≤ as
    -- m < n ≝ (S m) ≤ n.
    -- This approach does NOT work here cuz if q' ⊑ q
    -- then there typically are multiple possible direct successors of q'.
    _⊑_ : Rel Q ℓ
    q' ⊑ q = (q' ≡ q) ⊎ (q' ⋤ q)

    data SGState where
        root 
            : (h : (fin ℕ.zero) <∞ card)
            --^ *If* at least one element exists,...
            → SGState (nonzeroCardToZeroElem h) ((nonzeroCardToZeroElem h) ∷ [])
            --^ ...then there is a canonical root state with only that
            -- element explored (and the reflexive congruence on it).
            -- The list of normal forms is [ 0 ].
        choose 
            : (q : Q)
            → (h : IsNotMax (idx q))
            → (lc : LegalChoices q )
            → SGState (idxSuc h) (UpdateNFList q h lc)

    -- Macro.
    -- Given the data for an SGState successor s+ for s in q = (i , L , s),
    -- the index and NFList of s+ are already fixed as well.
    QSucc
        : {q : Q}
        → (h : IsNotMax (idx q))
        → (lc : LegalChoices q)
        → Q
    QSucc {q} h lc = (idxSuc h , UpdateNFList q h lc , choose q h lc)

--------------------------------------------------------------------------------
-- Substack (sub-choice-log) relation ⊑.
--------------------------------------------------------------------------------
    
    data _⋤_ where
        onechoice 
            : (q : Q) 
            → (h : IsNotMax (idx q))
            → (lc : LegalChoices q)
            → q ⋤ (idxSuc h , UpdateNFList q h lc , choose q h lc)
        multichoice
            : (q' q : Q)
            → (q' ⋤ q)
            → (h : IsNotMax (idx q))
            → (lc : LegalChoices q)
            → q' ⋤ (idxSuc h , UpdateNFList q h lc , choose q h lc)

    ⋤-trans : Transitive _⋤_
    --⋤-trans {q₁} {q₂} {q₃} q₁⋤q₂ q₂⋤q₃ = ?
    ⋤-trans {q₁} {q₂} {q₃@(i₃ , L₃ , s₃)} q₁⋤q₂ q₂⋤q₃@(onechoice q₂ h lc) = 
        multichoice q₁ q₂ q₁⋤q₂ h lc
    ⋤-trans {q₁} {q₂} {q₃@(i₃ , L₃ , s₃)} q₁⋤q₂ (multichoice q₂ q₄ q₂⋤q₄ h lc) =
        multichoice q₁ q₄ (⋤-trans q₁⋤q₂ q₂⋤q₄) h lc 

    -- Analogous to natural numbers: m < 1+n means m ≤ n,
    -- it holds q' ⋤ <some extension of q> → q' ⊑ q.
    -- This is FC-j in my notes.
    sublogLastChoice
        : {q' q : Q}
        → (h : IsNotMax (idx q))
        → (lc : LegalChoices q)
        → q' ⋤ QSucc h lc
        -- #TODO: what is better, the above macro or the full def below?
        --→ q' ⋤ (idxSuc h , UpdateNFList q h lc , choose q h lc)
        → q' ⊑ q
    sublogLastChoice {q'} {q} h lc (onechoice q h lc) = 
        let q'≡q = refl in
        inj₁ q'≡q
    sublogLastChoice {q'} {q} h lc (multichoice q' q q'⋤q h lc) = inj₂ q'⋤q

--------------------------------------------------------------------------------
-- Element representations.
--------------------------------------------------------------------------------
    
    -- Substack definition of element-already-chosen-in-a-state.
    -- In contrast to the index-based definition (`iElem`, used in PhCore.agda).
    -- An element is identified with the prefix of the choice log up to the
    -- point where that element is added to the congruence.
    sElem : Q → Set ℓ
    sElem q = Σ[ q' ∈ Q ](q' ⊑ q)

    getState : {q : Q} → sElem q → Q
    getState {q} (q' , q'⊑q) = q' -- Same as proj₁
    
    -- Convert from sElem-representation of an element to the number
    -- it has in the enumeration of A.
    getIdx : {q : Q} → sElem q → C
    getIdx {q} q' = idx (getState (q'))

    -- Convert from sElem-representation of an element to the A-term
    -- it represents.
    getEl : {q : Q} → sElem q → A
    getEl {q} q' = Signoid.idxToEl S (getIdx q')

    nextEl : {i : C} → (h : IsNotMax i) → A
    nextEl h = idxToEl (idxSuc h)
--------------------------------------------------------------------------------
-- Definitions of other auxiliary inductive types used in the construction
-- of states.
--------------------------------------------------------------------------------
    -- Predicate that tells that all arguments (via the _⊂_ relation)
    -- of the next element for which to choose its equalities
    -- are normal forms.
    AllArgsNormal
            : {i : C}
            → {L : NFList}
            → (s : SGState i L)
            → (h : IsNotMax i)
            → Set _
    AllArgsNormal {i} {L} s h = 
                (x : sElem (i , L , s))
                → ((getEl x) ⊂ (nextEl h)) 
                → (getIdx x) ∈ L

    -- Same as AllArgsNormal, but using the enumeration-index representation of
    -- elements.
    IAllArgsNormal
            : {i : C}
            → {L : NFList}
            → (s : SGState i L)
            → (h : IsNotMax i)
            → Set _
    IAllArgsNormal {i} {L} s h = 
                (x : C)
                → ((idxToEl x) ⊂ (nextEl h))
                → x ∈ L

    -- Predicate that the next element y has an x ⊂ y
    -- such that x is NOT a normal form.
    NormalisibleArg
            : {i : C}
            → {L : NFList}
            → (s : SGState i L)
            → (h : IsNotMax i)
            → Set _
    NormalisibleArg {i} {L} s h
            = Σ[ x ∈ sElem (i , L , s) ](
                ((getEl x) ⊂ (nextEl h))
                ×
                (getIdx x) ∉ L
                )

    -- Same as NormalisibleArg,
    -- but using the enumeration-index representation of elements.
    INormalisibleArg
            : {i : C}
            → {L : NFList}
            → (s : SGState i L)
            → (h : IsNotMax i)
            → Set _
    INormalisibleArg {i} {L} s h
            = Σ[ x ∈ C ](
                ((idxToEl x) ⊂ (nextEl h))
                ×
                (x ∉ L)
                )

    data LegalChoices where
        newNF 
            : {i : C}
            → {L : NFList}
            → (s : SGState i L)
            → (h : IsNotMax i)
            → (IAllArgsNormal s h)
            → LegalChoices (i , L , s)
        freeChoice
            : {i : C}
            → {L : NFList}
            → (s : SGState i L)
            → (h : IsNotMax i)
            → (IAllArgsNormal s h)
            → (Indices L)
            --^ (Index of) normal form to which we set the next element equal.
            → LegalChoices (i , L , s)
        forcedChoice
            : {i : C}
            → {L : NFList}
            → (s : SGState i L)
            → (h : IsNotMax i)
            → (INormalisibleArg s h)
            → LegalChoices (i , L , s)

    UpdateNFList (i , L , s) h (newNF s₁ _ x) = (idxSuc h) ∷ L
    UpdateNFList (i , L , s) h (freeChoice s₁ _ x x₁) = L
    UpdateNFList (i , L , s) h (forcedChoice s₁ _ x) = L

--------------------------------------------------------------------------------
-- Well-foundedness of _⋤_ and recursion principle for _⋤_.
--------------------------------------------------------------------------------

    rootLog : (h : (fin ℕ.zero) <∞ card) → Q
    rootLog h = ( nonzeroCardToZeroElem h 
                , nonzeroCardToZeroElem h ∷ [] 
                , root h)

    rootHasNoSublog
        : {q : Q}
        → {h : (fin ℕ.zero) <∞ card}
        → ¬ (q ⋤ rootLog h)
    rootHasNoSublog ()

    open import Induction.WellFounded as WF
    ⋤-wellFounded : WellFounded _⋤_
    ⋤-wellFounded (_ , L , root h) = 
        acc λ { q'⋤root → ⊥-elim (rootHasNoSublog q'⋤root) }
    ⋤-wellFounded (_ , L , choose q h lc) = acc f
        where
            f : {q' : Q} 
              → q' ⋤ (idxSuc h , UpdateNFList q h lc , choose q h lc) 
              → Acc _⋤_ q'
            f {q'} (onechoice q₁ h lc) = ⋤-wellFounded q₁
            f {q'} (multichoice q' q₁ q'⋤q₁ h lc) = 
                let rec = acc-inverse (⋤-wellFounded q₁) in
                rec q'⋤q₁

    -- #TODO: wfRec and wfRec-builder from the standard library might
    -- do this automatically. Skipped this for now.
    -- Current implementation mimics the <-rec function defined in
    -- the book "PROGRAM=PROOF" page 331 by Samuel Mimram (2025 version).
    ⋤-rec
        : {ℓ : Level}
        → (P : Q → Set ℓ)
        → ((q : Q) → ((q' : Q) → (q' ⋤ q) → P q') → P q)
        -- ^ If you can compute P q provided that P q' can be computed
        -- for all predecessors of q'...
        → (q : Q) → (P q)
        -- ^ ... then inductively we can compute P q for all q : Q.
    ⋤-rec P recurse q = lemma q (⋤-wellFounded q)
        where
            lemma : (q : Q) → (Acc _⋤_ q) → P q
            lemma q (acc allPredAcc) 
                = recurse q (λ q' → (λ q'⋤q → (lemma q' (allPredAcc q'⋤q))))
     
--------------------------------------------------------------------------------
-- Indices (first projections) of sublogs are smaller than of the superlog.
--
-- This is proven via ⋤-rec with P ≔ sublogSmallerIdxOUT.
-- See `sublogSmallerIdx` for the fa¢ade function that is to be used in
-- practice.
--------------------------------------------------------------------------------

    sublogSmallerIdxOUT : Q → Set ℓ
    sublogSmallerIdxOUT q = (q' : Q) → (q' ⋤ q) → (idx q') <C (idx q)

    sublogSmallerIdxRec
        : (q : Q)
        → ( (q' : Q) → (q' ⋤ q) → (sublogSmallerIdxOUT q'))
        → sublogSmallerIdxOUT q
    sublogSmallerIdxRec q _ q' (onechoice q₁ h lc) = endoSucBigger h
    sublogSmallerIdxRec q recurse q' q'⋤q@(multichoice q' q₁ q'⋤q₁ h lc) = 
        let rec = recurse q₁ (onechoice q₁ h lc)
        in
        let idxq'<idxq₁ = rec q' q'⋤q₁
        in 
        let idxq₁<idxq : (idx q₁) <C (idx q)
            idxq₁<idxq = endoSucBigger h
        in
        cardTo<Trans {card} idxq'<idxq₁ idxq₁<idxq

    sublogSmallerIdx
        : {q' q : Q}
        → q' ⋤ q
        → (idx q') <C (idx q)
    sublogSmallerIdx {q'} {q} q'⋤q = 
        ⋤-rec sublogSmallerIdxOUT sublogSmallerIdxRec q q' q'⋤q

--------------------------------------------------------------------------------
-- Suffix of normal-forms-list relation.
--------------------------------------------------------------------------------
    

    -- Lemma A3 in my 12 Dec 2025 notes.
    -- If q' ⋤ (L, choose q' lc), then L must be an extension
    -- of the normal forms of q.
    -- This is a special case (and auxiliary lemma) 
    -- of `multichoiceSuffix` below.
    onechoiceSuffix
        : {i : C}
        → {L : NFList}
        → {s : SGState i L}
        → {h  : IsNotMax i}
        → {lc : LegalChoices (i , L , s)}
        → (i , L , s) 
          ⋤ 
          (idxSuc h , UpdateNFList (i , L , s) h lc , choose (i , L , s) h lc)
        → L ≼ UpdateNFList (i , L , s) h lc
    onechoiceSuffix {_} {L} {s} {_} {newNF s _ x} q⊑q = Suffix.there ≼-refl
    onechoiceSuffix {_} {L} {s} {_} {freeChoice s _ x x₁} q⊑q = ≼-refl
    onechoiceSuffix {_} {L} {s} {_} {forcedChoice s _ x} q⊑q = ≼-refl

    -- When adding more choices to a choice log, the new list of normal forms
    -- is an extension of the original list. 
    multichoiceSuffix
        : {i' i : C}
        → {L' L : NFList}
        → {s' : SGState i' L'}
        → {s  : SGState i L}
        → (i' , L' , s') ⊑ (i , L , s)
        → L' ≼ L
    -- Easy case: given q'⊑q where both are the root,
    -- we know both have as NFList simply [0].
    -- Only hurdle is that Agda doesn't immediately see that 
    --      nonzeroCardToZeroElem h' = nonzeroCardToZeroElem h
    multichoiceSuffix {i'} {i} {L'} {L} {root h'} {root h} q'⊑q = 
        let zeroh≡zeroh' = thereIsOneZero' {card} h h' in
        let ref = ≼-refl in
        subst (λ k → Suffix _≡_ (k ∷ []) (nonzeroCardToZeroElem h ∷ [])) 
            zeroh≡zeroh' ref
    -- Any q'⊑q where q has only the root element and q' at least
    -- two elements (`choose` as topmost constructor) is impossible.
    multichoiceSuffix {i'} {i} {L'} {L} {choose q h lc} {root k} (inj₁ ())
    multichoiceSuffix {i'} {i} {L'} {L} {choose q h lc} {root k} (inj₂ ())
    -- q'⊑q gives two cases. In the first case, q'≡q,
    -- i.e., (L' , s'_ = (L , choose q lc).
    -- Then trivially L' ≡ L as well, and ≼ is reflexive.
    multichoiceSuffix {i'} {i} {L'} {L} {s'} {choose q'' h lc} (inj₁ refl) 
        = ≼-refl
    -- In the other case we have q`⋤q (strict sublog).
    -- First subcase: q' = (L' , s') has only one choice fewer than q.
    -- Hence we are in the onechoice situation, which we already proved above.
    multichoiceSuffix {i'} {i} {L'} {L} {s'} {choose (i' , L' , s') h lc} 
        (inj₂ q'⋤q@(onechoice (i' , L' , s') h' lc)) =
        onechoiceSuffix {i'} {L'} {s'} {h'} {lc} q'⋤q
    -- Second subcase: q has several choices on top of those in q'.
    -- Then we have:
    --      (1) q = choose q₁ lc
    --      (2) q' ⊑ q₁
    --      (3) q₁ = (L₁ , s₁)
    --  We can recurse on (2) to obtain 
    --      (4) L' ≼ L₁
    --  and the onechoiceSuffix lemma on (1) will give
    --      (5) L₁ ≼ L
    --  Transitivity of ≼ on (4) and (5) then gives the desired
    --      (6) L' ≼ L
    multichoiceSuffix {i'} {i} {L'} {L} {s'} {choose q₁ h lc} 
        (inj₂ (multichoice q' q₁@(i₁ , L₁ , s₁) q'⋤q₁ h₁ lc)) = 
        let q'⊑q₁ = inj₂ q'⋤q₁ in
        let L'≼L₁ = multichoiceSuffix {i'} {i₁} {L'} {L₁} {s'} {s₁} q'⊑q₁ in
        let L₁≼L  = onechoiceSuffix {i₁} {L₁} {s₁} {h} {lc} (onechoice q₁ h lc) 
        in
        ≼-trans L'≼L₁ L₁≼L
        
    -- Same as above, but now with states wrapped into single elements.
    multichoiceSuffix'
        : {q' q : Q}
        → q' ⊑ q
        → (nflist q') ≼ (nflist q)
    multichoiceSuffix' {i' , L' , s'} {i , L , s}
        = multichoiceSuffix {i'} {i} {L'} {L} {s'} {s}

--------------------------------------------------------------------------------
-- Auxiliary lemmas needed to compute normal forms.
--------------------------------------------------------------------------------
--#TODO: rename the lemmas. Current names are the ones used in my paper notes.

    FC-a 
        : {i : C}
        → (h₁ : IsNotMax i)
        → (h₂ : IsNotMax i)
        → (idxSuc h₁ ≡ idxSuc h₂)
    FC-a {i} h₁ h₂ = endoSucUnique h₁ h₂


    -- Lemma FC-b : if there is an enumeration-index i smaller than
    -- the index of the last element added to choicelog q,
    -- then there exists a STRICT subchoicelog 
    -- of q where i was the last element added.
    -- (Strict subchoicelog is stronger than an sElem: it uses ⋤ i.o. ⊑).
    getSubLog
        : (q : Q)
        → (i : C)
        → (i <C idx q)
        → Σ[ q' ∈ Q ]( (q' ⋤ q) × (i ≡ idx q'))
    -- The hypothesis i<iq is impossible if q is a root log:
    -- i < nonzeroCardToZeroElem h is impossible.
    getSubLog (iq , L , root h) i i<iq = ⊥-elim (nothingIs<0 i h i<iq)
    getSubLog (iq , L , choose q' h lc) i i<iq 
        with cardToDecidableEq card i (idx q')
    -- If i = iq' then q' itself is already the choicelog we seek!
    ... | yes i≡iq' = (q' , onechoice q' h lc , i≡iq')
    -- In the last case, i ≢ iq', so (1) i > iq' xor (2) i < iq'. 
    -- But i < iq and iq = 1 + iq', so if i > iq' then 1 + iq' > i > iq',
    -- which means that (1 + iq') is at least 2 greater than iq'; contradiction.
    -- So only option (2) remains: i < iq'. Then we can recurse getSubLog
    -- and use transitivity of ⋤ (a sublog of q' is also a sublog of q).
    ... | no  i≢iq' with (cardTo<Dec {card} i (idx q'))
    ... | yes (i<iq') = 
        let (q'' , q''⋤q' , iq''≡i) = getSubLog q' i i<iq' in
        let q'⋤q = onechoice q' h lc in
        (q'' , ⋤-trans q''⋤q' q'⋤q , iq''≡i)
    -- The impossible case i > iq':
    ... | no  (i≮iq') = 
        let iq'<i = n≮m→n≢m→m<n i≮iq' i≢iq' in
        ⊥-elim (j<i<Sj-impossible {card} {i} {idx q'} {h} i<iq iq'<i)
    
    -- This is FC-e in my notes.
    argSmallerIdx
        : (q : Q)
        → (x : A)
        → (x ⊂ el q)
        → elToIdx x <C elToIdx (el q)
    argSmallerIdx q x x⊂q = 
        Signoid.subrelat S x (el q) x⊂q

    -- Incrementing the index of a ChoiceLog gives the same index
    -- as adding a choice to the ChoiceLog and projecting the index.
    -- #TODO: remove? This is completely trivial, at type level
    -- Agda only allows me to write `idxSuc h` in the RHS and `idx` is just
    -- `proj₁`...
    nextIdxUnique
        : (q' : Q)
        → (h : IsNotMax (idx q'))
        → (lc : LegalChoices q')
        → idxSuc h ≡ idx (idxSuc h , UpdateNFList q' h lc , choose q' h lc)
    nextIdxUnique q' h lc = refl
    
    -- This lemma bottles down to elToIdx ∘ idxToEl = id.
    -- The difficulty is that one needs to unfold the definitions to see this.
    nextIdxUnique2
        : {i : cardToSet card}
        → (h : IsNotMax i)
        → Signoid.elToIdx S (nextEl h) ≡ idxSuc h
    nextIdxUnique2 {i} h = invIdxElIdx (endoSuc h)

--------------------------------------------------------------------------------
-- Normal form lists are always sorted.
--------------------------------------------------------------------------------

    -- If all elements in a list are ≤ x,
    -- and if x ≤ y, then all elements in the list are ≤ y
    -- (provided _≤_ is transititive).
    All-with-trans
        : {ℓ : Level}
        → {A : Set ℓ}
        → {_≤_ : Rel A ℓ} 
        → {x y : A}
        → {L : List A}
        → x ≤ y
        → Transitive _≤_
        → All (_≤ x) L
        → All (_≤ y) L
    All-with-trans {ℓ} {A} {_≤_} {x} {y} {[]} _ _ All≤x = All.[]
    All-with-trans {ℓ} {A} {_≤_} {x} {y} {a ∷ L} x≤y trans (a≤x All.∷ All≤x) = 
        let rec : All (_≤ y) L
            rec = All-with-trans {ℓ} {A} {_≤_} {x} {y} {L = L} x≤y trans All≤x 
        in
        let a≤y : a ≤ y
            a≤y = trans a≤x x≤y
        in
        a≤y All.∷ rec

    -- See lastNFIsBiggest below for the fa¢ade function that one should use in
    -- practise. It is defined via ⋤-rec (WF-recursion on
    -- subchoicelog-relation),
    -- The type below is the `P` argument in ⋤-rec.
    lastNFIsBiggestOUT : Q → Set _
    lastNFIsBiggestOUT q = 
        (h : IsNotMax (idx q)) → (All (_<C idxSuc h) (nflist q))

    -- Actual recursive implementation, to be fed into ⋤-rec.
    -- #TODO: LOT of duplicate code between the different cases.
    lastNFIsBiggestRec
        : (q : Q)
        → (
            (q' : Q) → (q' ⋤ q) → lastNFIsBiggestOUT q'
          )
        → lastNFIsBiggestOUT q
    lastNFIsBiggestRec (i , L , (root k)) _ h = (endoSucBigger h) All.∷ All.[]
    --lastNFIsBiggestRec (i , L , choose q' h' lc') recurse h = {! !}
    lastNFIsBiggestRec q@(i , L , choose q' h' lc@(newNF s h₁ x)) recurse h =
    -- h' and h₁ both say that `IsNotMax (idx q')`, and could be contracted
    -- together.
        let L' : NFList
            L' = nflist q'
        in
        let q'⋤q : q' ⋤ q
            q'⋤q = onechoice q' h' lc
        in
        let rec : All (_<C (idxSuc h')) L'
            rec = recurse q' q'⋤q h'
        in
        let ISh'<ISh : (idxSuc h') <C (idxSuc h)
            ISh'<ISh = endoSucBigger h
        in
            (endoSucBigger h) 
            All.∷ 
            (All-with-trans {0ℓ} {C} {_<C_} {idxSuc h'} {idxSuc h} {L'} 
                            ISh'<ISh (cardTo<Trans {card}) rec)
    -- In the freeChoice and forcedChoice cases, the NFList is not updated,
    -- so L' = L (definitional equality).
    lastNFIsBiggestRec q@(i , L , choose q' h' lc@(freeChoice s h₁ x x₁)) 
                       recurse h =
        let L' : NFList
            L' = nflist q'
        in
        let q'⋤q : q' ⋤ q
            q'⋤q = onechoice q' h' lc
        in
        let rec : All (_<C (idxSuc h')) L
            rec = recurse q' q'⋤q h'
        in
        let ISh'<ISh : (idxSuc h') <C (idxSuc h)
            ISh'<ISh = endoSucBigger h
        in
        All-with-trans {0ℓ} {C} {_<C_} {idxSuc h'} {idxSuc h} {L'} 
                            ISh'<ISh (cardTo<Trans {card}) rec
    -- Proof of the forcedChoice case is *exactly* the same as the
    -- freeChoice case.
    lastNFIsBiggestRec q@(i , L , choose q' h' lc@(forcedChoice s h₁ x))
                    recurse h = 
        let L' : NFList
            L' = nflist q'
        in
        let q'⋤q : q' ⋤ q
            q'⋤q = onechoice q' h' lc
        in
        let rec : All (_<C (idxSuc h')) L
            rec = recurse q' q'⋤q h'
        in
        let ISh'<ISh : (idxSuc h') <C (idxSuc h)
            ISh'<ISh = endoSucBigger h
        in
        All-with-trans {0ℓ} {C} {_<C_} {idxSuc h'} {idxSuc h} {L'} 
                            ISh'<ISh (cardTo<Trans {card}) rec

    -- The most recently added NF-representative has a greater
    -- enumeration-index than the representatives of all earlier NFs.
    lastNFIsBiggest
        : (q : Q)
        → (h : IsNotMax (idx q))
        → All (_<C (idxSuc h)) (nflist q)
    lastNFIsBiggest q h =  ⋤-rec lastNFIsBiggestOUT lastNFIsBiggestRec q h

    -- Output type of WF-recursion of `nflistsSorted`.
    -- Used as the `P` argument to `⋤-rec`.
    nfListsSortedOUT : Q → Set
    nfListsSortedOUT q = AllPairs (_>C_) (nflist q)

    -- Helper function of `nfListsSorted` below.
    -- #TODO: LOT of duplicate code between the different cases.
    nfListsSortedRec
        : (q : Q)
        → ((q' : Q) → (q' ⋤ q) → nfListsSortedOUT q')
        → nfListsSortedOUT q
    nfListsSortedRec (i , L , root h) _ = All.[] AllPairs.∷ AllPairs.[]
    nfListsSortedRec q@(i , L , choose q' h' lc@(freeChoice s h x x₁)) recurse =
        let L' : NFList 
            L' = nflist q'
        in
        let q'⋤q : q' ⋤ q
            q'⋤q = onechoice q' h' lc
        in
        let rec : AllPairs _>C_ L'
            rec = recurse q' (q'⋤q)
        in
        rec -- Works because L' ≐ L in the freeChoice case.
    -- The forcedChoice case uses exactly the same proof as the freeChoice case.
    nfListsSortedRec q@(i , L , choose q' h' lc@(forcedChoice s h x)) recurse =
        let L' : NFList 
            L' = nflist q'
        in
        let q'⋤q : q' ⋤ q
            q'⋤q = onechoice q' h' lc
        in
        let rec : AllPairs _>C_ L'
            rec = recurse q' (q'⋤q)
        in
        rec
    -- In the newNF case, there actually is a new element added to L'
    -- to produce L. The lemma `lastNFIsBiggest` proves that this new element
    -- is greater than the other elements,
    -- and recursion will handle the tail of the list.
    nfListsSortedRec q@(i , L , choose q' h' lc@(newNF s h x)) recurse = 
        let L' : NFList 
            L' = nflist q'
        in
        let q'⋤q : q' ⋤ q
            q'⋤q = onechoice q' h' lc
        in
        let rec : AllPairs _>C_ L'
            rec = recurse q' (q'⋤q)
        in
        let lastBig : All (_>C_ (endoSuc h')) L'
            lastBig = lastNFIsBiggest q' h'
        in
        lastBig AllPairs.∷ rec

    -- Enumeration-indices of NF representatives of newer NFs are always greater
    -- than that of earlier NF's representatives.
    -- I.e. i <C j if j appears later in the NFList than i.
    nfListsSorted
        : (q : Q)
        → AllPairs _>C_ (nflist q)
    nfListsSorted = ⋤-rec nfListsSortedOUT nfListsSortedRec

--------------------------------------------------------------------------------
-- All representatives x of normal forms, as they occur in a NFList
-- of a state q, have `x ≤ (idx q)`.
--
-- This is proven via well-founded induction via ⋤-rec with P ≔ nfsAre≤OUT.
--------------------------------------------------------------------------------
    nfsAre≤OUT : Q → Set
    nfsAre≤OUT q = (j : C) → (j ∈ nflist q) → (j ≡ idx q) ⊎ (cardTo< j (idx q))

    -- If an element is in a list, but it is not the first
    -- element, then it must be in the suffix.
    notFirstThenInSuffix
        : {X : Set}
        → {xs : List X}
        → {a x : X}
        → a ∈ (x ∷ xs)
        → a ≢ x
        → a ∈ xs
    notFirstThenInSuffix {X} {xs} {a} {x} (Any.here a≡x) a≢x = ⊥-elim (a≢x a≡x)
    notFirstThenInSuffix {X} {xs} {a} {x} (Any.there a∈xs) a≢x = a∈xs
    
    -- Lemma for nfsAre≤Rec below.
    -- It handles all the cases where j∈L implies j∈L'
    -- (where L' = nflist q', and q' is the choicelog without the last choice).
    nfsAre≤RecLemma
        : (j  : C)
        → (q' : Q)
        → (h' : IsNotMax (idx q'))
        → (lc : LegalChoices q')
        → (j ∈ (nflist q'))
        → ( (q* : Q) → q* ⋤ (QSucc h' lc) → nfsAre≤OUT q*)
        → (j ≡ idx (QSucc h' lc)) ⊎ (j <C idx (QSucc h' lc))
    nfsAre≤RecLemma j q' h' lc j∈L' recurse = 
        let q : Q
            q = QSucc h' lc
        in
        let q'⋤q : q' ⋤ q
            q'⋤q = onechoice q' h' lc
        in
        let j≤idxq' = recurse q' q'⋤q j j∈L'
        in
        let idxq'<idxq : (idx q') <C (idx q)
            idxq'<idxq = sublogSmallerIdx q'⋤q
        in
        let j<idxq : j <C (idx q)
            j<idxq = leqSmallerTrans j≤idxq' idxq'<idxq 
        in
        inj₂ j<idxq

    nfsAre≤Rec 
        : (q : Q)
        → ( (q' : Q) → q' ⋤ q → nfsAre≤OUT q')
        → nfsAre≤OUT q
    nfsAre≤Rec (i , L , root h) recurse j (Any.here j≡0) = inj₁ j≡0
    nfsAre≤Rec q@(i , L , choose q' h' lc@(newNF s' h₁ x)) recurse j j∈L 
            -- Make case distinction on whether `j ≡ head L` or not.
            -- Note that (head L) ≐ idxSuc h' ≐ idx q by def of UpdateNFList.
            with cardToDecidableEq card j (idxSuc h')
    ... | yes j≡idxq = inj₁ j≡idxq
    ... | no  j≢idxq = 
            let j∈L' = notFirstThenInSuffix j∈L j≢idxq
            in
            nfsAre≤RecLemma j q' h' lc j∈L' recurse
    -- The last two cases are easier since L' = L, so j∈L → j∈L' already.
    nfsAre≤Rec (_ , L , choose q' h' lc@(freeChoice _ _ _ _)) recurse j j∈L =
            nfsAre≤RecLemma j q' h' lc j∈L recurse
    nfsAre≤Rec (_ , L , choose q' h' lc@(forcedChoice _ _ _)) recurse j j∈L =
            nfsAre≤RecLemma j q' h' lc j∈L recurse

    -- The enumeration-indices in a NFList of a choice-log
    -- are ≤ than the enum-idx of the last element added to the choice-log.
    -- This is FC-i in my notes (notes FC3(3)).
    nfsAre≤
        : (q : Q)
        → (j : C)
        → j ∈ nflist q
        → j ≡ (idx q) ⊎ (cardTo< j (idx q))
    nfsAre≤ = ⋤-rec nfsAre≤OUT nfsAre≤Rec
    

--------------------------------------------------------------------------------
-- Normal form computation algorithm
--
-- Defined via Well-Founded induction on ⋤,
-- using  ⋤-rec with P ≔ NFOUT.
--------------------------------------------------------------------------------

    NFOUT : Q → Set _
    NFOUT q' = (q : Q) → q' ⋤ q → Indices (nflist q')

    nfTransposedRec 
        : (q' : Q)
        --^ Subchoicelog whose normal form we want.
        -- The complete ChoiceLog q is hidden in NFOUT
        -- (instead of being the first argument to this function)
        -- hence the name `nfTransposedRec`.
        → ((q'' : Q) → q'' ⋤ q' → NFOUT q'')
        --^ Ability to make recursive calls.
        → NFOUT q'

    -- The normal form of the root element is always the root
    -- element itself, and is always the first normal form in the ChoiceLog,
    -- so has index 0 in the NFList.
    nfTransposedRec (i' , L' , root h') recurse q q'⋤q = Fin.zero
    -- newNF case is easy: the element itself is already in normal form,
    -- and the most recent entry in the NFList.
    -- Agda knows that L' is of the form (y ∷ L'') by definition
    -- of UpdateNFList, so we don't need to prove that L' is nonempty.
    nfTransposedRec 
        q'@(i' , L' , choose q'' h'' (newNF s h noCoerc)) 
        recurse q q'⋤q = Fin.zero
    -- freeChoice case is easy, since the freeChoice constructor
    -- already stores the desired index.
    nfTransposedRec 
        q'@(i' , L' , choose q'' h'' (freeChoice s h noCoerc iₙ)) 
        recurse q q'⋤q = iₙ
    -- The forcedChoice case is the hardest.
    -- Let y be the most recent element added to q'.
    -- Input: witness x ⊂ y s.t. x is not in normal form.
    -- Desired output: the normal form of y' ≔ coerc(y, x, nf(x)).
    -- Do:
    --  1. Recurse to compute nf(x).
    --  2. Use the coerc attribute of the Signoid to get y' 
    --      (represented by q* in code below).
    --  3. Recurse again to normalise y'.
    nfTransposedRec 
        q'@(i' , L' , choose q'' h'' 
        lc''@(forcedChoice {i''} {L''} s'' h''' (ix , x⊂nextq'' , ix∉L') )) 
        recurse q q'⋤q =
        let x = idxToEl ix in
        let h'''≡h'' = IsNotMax-irrel i'' h''' h'' in
        -- There is h'' and h''', which are not judgementally equal
        -- but definitely propositionally equal since `IsNotMax i''` is a prop.
        let x⊂nextq''h'' = 
                subst (λ v → (x ⊂ nextEl v)) (h'''≡h'') x⊂nextq'' 
        in
        -- The LHS of the following term is actually 
        -- elToIdx (idxToEl ix), not ix. However, these functions are inverse!
        -- Same problem applies to the RHS.
        let ix<iq'-almost = Signoid.subrelat S x (el q') x⊂nextq''h'' in
        -- Remove the invese functions from the LHS:
        let ixInv = invIdxElIdx ix in
        let ix<iq'-2 = subst (λ i → cardTo< i _) ixInv ix<iq'-almost in
        -- Now from the RHS:
        let iq'Inv = invIdxElIdx (idxSuc h'') in
        let ix<iq'-3 = subst (λ i → cardTo< _ i) iq'Inv ix<iq'-2 in
        -- Get the subchoicelog corresponding to the element x.
        let (qx , qx⋤q' , ix≡idxqx) = getSubLog q' ix ix<iq'-3 in
        let idxqx : C
            idxqx = idx qx
        in
        -- Get the normal form of x for any desired superlog of qx
        -- (this is the type NFOUTx').
        let NFOUTx' = recurse qx qx⋤q' in
        -- Specialise to the superlog q', which will give us 
        -- ix' as in index in L' (where L' is the NFList of qx, the choice log
        -- with x as last choice).
        -- From here we can prove that ix' < ix, 
        -- which we need to call Signoid.coerc to coerce along NF(X) ≈ x.
        let Lx : NFList
            Lx = nflist qx
        in
        let ix'-in-Lx : Indices Lx
            ix'-in-Lx = NFOUTx' q' qx⋤q' 
        in 
        let ix' : C
            ix' = lookup Lx ix'-in-Lx
        in
        let ix'∈Lx : ix' ∈ Lx
            ix'∈Lx = ∈-lookup {xs = Lx} ix'-in-Lx 
        in
        -- This is `ix'≡ix ⊎ ix'<ix` (but using cardTo<)
        let ix'≤ix : (ix' ≡ ix) ⊎ (cardTo< ix' ix)
            ix'≤ix = subst (λ k → ix' ≡ k ⊎ cardTo< ix' k) 
                           (sym ix≡idxqx) (nfsAre≤ qx ix' ix'∈Lx)
        in
        -- ix' cannot be ix, because ix' ∈ Lx
        -- but x is not a normal form, which was proven via ix ∉ L'
        -- (and x is an element in q', 
        -- and qx the corresponding subchoicelog of q',  so Lx ≼ L')
        -- So ix' ≡ ix would give ix ∈ Lx, a contradiction.
        let Lx≼L' : Lx ≼ L'
            Lx≼L' = multichoiceSuffix' {qx} {q'} (inj₂ qx⋤q')
        in
        let ix∉Lx : ix ∉ Lx
            ix∉Lx = notInListThenNotInSuffix Lx≼L' ix∉L' 
        in
        let ix'≢ix : ix' ≢ ix
            ix'≢ix = λ ix'≡ix 
                     → ⊥-elim (ix∉Lx (subst (λ j → j ∈ Lx) ix'≡ix ix'∈Lx)) 
        in
        let ix'<ix : cardTo< ix' ix
            ix'<ix = elimCaseLeft ix'≤ix ix'≢ix 
        in
        let invix' : C
            invix' = elToIdx (idxToEl ix')
        in
        let ix'≡invix' : ix' ≡ invix'
            ix'≡invix' = sym (invIdxElIdx ix')
        in
        let invix'<ix : cardTo< invix' ix
            invix'<ix = subst (λ k  → cardTo< k ix) ix'≡invix' ix'<ix
        in
        let ix≡elToIdxx : ix ≡ (elToIdx x)
            ix≡elToIdxx = sym (invIdxElIdx ix)
        in
        let invix'<elToIdxx : cardTo< invix' (elToIdx x)
            invix'<elToIdxx = subst (λ k  → cardTo< invix' k) 
                                    ix≡elToIdxx 
                                    invix'<ix
        in
        let coercOut : Σ[ y' ∈ A ](
                cardTo< (elToIdx y') (elToIdx (nextEl h'')))
            coercOut = Signoid.coerc S (nextEl h'') 
                x x⊂nextq''h'' (idxToEl ix') invix'<elToIdxx
        in
        let (y' , idxq*<idxnextq'') = coercOut in
        let idxq* = elToIdx y' in
        -- The A-is-enumerable bijection elToIdx ∘ idxToEl = id causes the
        -- need a subst here: (Signoid.elToIdx S (nextEl h'')) != (endoSuc h'')
        let k : cardTo< idxq* (idx q')
            k = subst (λ j → cardTo< idxq* j) 
                      (nextIdxUnique2 h'') idxq*<idxnextq''
        in
        let (q* , q*⋤q' , idxq'≡idxq*) = getSubLog q' idxq* k
        in
        let L* = nflist q* in
        let iqn-in-L* : Indices L*
            iqn-in-L* = (recurse q* q*⋤q') q' q*⋤q' 
        in
        let L*≼L' : L* ≼ L' 
            L*≼L' = multichoiceSuffix' (inj₂ q*⋤q')
        in
        let iqn-in-L' : Indices L'
            iqn-in-L' = suffixIdxInclusion L*≼L' iqn-in-L* 
        in
        iqn-in-L'

